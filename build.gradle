buildscript {
    repositories {
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Modmuss50"
            url = "http://maven.modmuss50.me/"
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'ModsIOUpload:ModsIOUpload:+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'net.cazzar.gradle.modsio'

version = "1.4.0"
group = "the_fireplace.grandeconomy"
archivesBaseName = "GrandEconomy-1.14.4"

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    mappings channel: 'snapshot', version: '20191124-1.14.3'
    
    runs {
        client = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.14.4-28.1.114'
    compile 'org.spongepowered:spongeapi:7.1.0'
    compile (group: "com.forgeessentials", name: "forgeessentials", version: "1.4.6-SNAPSHOT", classifier: "dev", changing: true) {
        exclude module: "unspecified"
    }
    compile "org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT"
    compile group: "net.milkbowl.vault", name: "VaultAPI", version: "1.7"
    compile name:'enderpay-1.12.2-1.0.1.0-deobf'
}

repositories {
    jcenter()
    mavenCentral()
    // Sponge
    maven { url 'https://repo.spongepowered.org/maven' }
    //Forge Essentials
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    //Forge Essentials 2
    maven {
        name = "sk89q"
        url = "http://maven.sk89q.com/repo/"
    }
    //Spigot
    maven { url = ("https://hub.spigotmc.org/nexus/content/groups/public/") }
    // Vault
    maven { url 'https://jitpack.io' }
    flatDir {
        dirs 'libs'
    }
}

jar {
    manifest {
        attributes(["Specification-Title": "grandeconomy",
                    "Specification-Vendor": "The_Fireplace",
                    "Specification-Version": "24.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title": "Grand Economy",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" : "The_Fireplace",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives jar
    archives deobfJar
}

curseforge {
  apiKey = project.hasProperty("curseForgeApiKey")? project.curseForgeApiKey : "empty"
  project {
    id = '312141'
    changelog = file('changelog.txt')
    releaseType = 'beta'
    addGameVersion '1.14.4'
	addGameVersion 'Java 8'
    mainArtifact(jar) {
        displayName = jar.archiveName.replace(".jar", "")
    }
    addArtifact(deobfJar) {
        displayName = deobfJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
  }
}

modsIO {
    key = project.hasProperty("modsioApiKey")? project.modsioApiKey : "empty"
    project{
		artifact = new File(jar.destinationDir, archivesBaseName+"-"+version+".jar")
		modid = "1278"
		minecraft = "1.14.4"
		try{changelog = file('changelog.txt').text}catch(Exception e){changelog=""}
		tag = ""
		current = true
    }
}
